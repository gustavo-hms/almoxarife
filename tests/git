#!/usr/bin/env fennel

(local fmt string.format)

(λ fail-with-code-and-msg [code msg]
  (io.stderr:write msg)
  (os.exit code))

(λ assert-eq-env [description ?value env-var-name error-code]
  "Assert the provided value is equal to the environment variable whose name is
  provided. If not, print a message to stderr containing the provided description
  and exit with the provided error code"
  (let [env-var (os.getenv env-var-name)]
    (when (and env-var (not= ?value env-var))
      (fail-with-code-and-msg error-code
                              (fmt "wrong %s: %s; expecting %s" description
                                   ?value env-var)))))

(let [fail (os.getenv :ALMOXARIFE_TEST_FAIL)]
  (when fail
    (fail-with-code-and-msg 1 fail)))

(assert-eq-env "CWD" (: (io.popen :pwd) :read) :ALMOXARIFE_TEST_CWD 2)

(case (. arg 1)
  :clone (do
           (assert-eq-env "git URL" (. arg 2) :ALMOXARIFE_TEST_LOCATION 4)
           (assert-eq-env "repo path" (. arg 3) :ALMOXARIFE_TEST_REPO_PATH 5)
           (os.execute (fmt "mkdir -p %s"
                            (os.getenv :ALMOXARIFE_TEST_REPO_PATH))))

  :pull (if (os.getenv :ALMOXARIFE_TEST_PULL_FAIL)
            (fail-with-code-and-msg 6 "can't pull changes")
            (when (> 1 (length arg))
              (fail-with-code-and-msg 7 (.. "unexpected argument: " (. arg 2)))))

  :rev-parse (if (os.getenv :ALMOXARIFE_TEST_REV_PARSE_FAIL)
                 (fail-with-code-and-msg 8 "can't retrieve commit SHA")

                 (and (= (os.getenv :ALMOXARIFE_TEST_PLUGIN_UPDATE) :1)
                      ;; We need to know if this is the first or the second call to
                      ;; rev-parse. To do so, we create a dummy file in the first run.
                      ;; Hacky, but works.
                      (os.execute "ls rev-parse.txt"))
                 (print "ghijkl")

                :else
                (do
                  (print "abcdef")
                  (os.execute "touch rev-parse.txt")))

  :log (if (os.getenv :ALMOXARIFE_TEST_LOG_FAIL)
           (fail-with-code-and-msg 9 "can't get log of changes")
           (print "abcdef Some change\nghijk Other change"))

  command (fail-with-code-and-msg 10 (.. "unexpected subcommand: " command)))
